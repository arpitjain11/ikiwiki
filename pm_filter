#!/usr/bin/perl -i -p

BEGIN {
	$prefix=shift;
	$ver=shift;
	$libdir=shift;
}

if (/INSTALLDIR_AUTOREPLACE/) {
	$_=qq{our \$installdir="$prefix";};
}
elsif (/VERSION_AUTOREPLACE/) {
	$_=qq{our \$version="$ver";};
}
elsif (/^use lib/) {
	# The idea here is to figure out if the libdir the Makefile.PL
	# was configured to use is in perl's normal search path.
	# If not, hard code it into ikiwiki.
	if ((grep { $_ eq $libdir } @INC) &&
            (! exists $ENV{PERL5LIB} || ! length $ENV{PERL5LIB} ||
	     $ENV{PERL5LIB} ne $libdir)) {
		$_="";
	}
	else {
		$_="use lib '$libdir';\n";
	}
}
elsif ($. == 1 && ($ENV{NOTAINT} || ! exists $ENV{NOTAINT}) && m{^(#!.*perl.*?) -T$}) {
	$_=qq{$1\n};
}
elsif (/^\$ENV{PATH}="(.*)";/) {
	$_="\$ENV{PATH}=\"$1:$prefix/bin\";\n";
}
